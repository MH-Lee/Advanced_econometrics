DATA ip;
			INFILE 'c:\data\ip.prn';
			INPUT mon ip;
			ipg = DIF(LOG(ip))*100;
RUN;
DATA sp;
			INFILE 'c:\data\sp500.prn';
			INPUT mon sp;
			spg = DIF(LOG(sp))*100;
RUN;
DATA ex01;
	MERGE ip sp;
	BY mon;
	int = 1;
	WHERE 19570101 < mon < 20120801;
RUN;
DATA ex011;
	SET ex01;
	IF mon > 20061231 THEN DELETE;
RUN;
DATA ex012;
	SET ex01;
	IF mon < 20070101 THEN DELETE;
RUN;
DATA st01;
	SET ex01;
	IF mon < 20070101 THEN DO;
		int1 = 1;
		int2 = 0;
		spg1 = spg;
		spg2 = 0;
		ipg1 = ipg;
		ipg2 = 0;
	END;
	ELSE DO;
		int1 = 0;
		int2 = 1;
		spg1 = 0;
		spg2 = spg;
		ipg1 = 0;
		ipg2 = ipg;
	END;
RUN;
PROC IML;
	START reg;
				n = NROW(x);
				k = NCOL(x);
				df = n-k;
				xpx = x`*x;
				xpy = x`*y;
				xpxi = INV(xpx);
				b = xpxi*xpy;
				yhat = x*b;
				e = y-yhat;
				sse = e`*e;
				mse = sse/df;
				covb = mse#xpxi;
				stdb = SQRT(VECDIAG(covb));
				t = b/stdb;
				probt = 1- PROBF(t#t,1,df);
				PRINT "result of regression are" b stdb t probt;
				e1 = LAG(e,1);
				em = e-e1;
				em[1,1] = 0;
				emsq = em`*em;
				dw=emsq/sse;
				PRINT "durbin watson-d is" dw;
	FINISH reg;
	USE ex01;
		READ all VAR{ipg} into y;
		READ all VAR{int spg} into x;
	RUN reg;
PROC IML;
	START joint;
				n = NROW(x);
				k = NCOL(x);
				df = n-k;
				xpx = x`*x;
				xpy = x`*y;
				xpxi = INV(xpx);
				b = xpxi*xpy;
				yhat = x*b;
				e = y-yhat;
				sse = e`*e;
				mse = sse/df;
				br=rr*b;
				q=nrow(rr);
				brr=br-r;
				tr=rr*xpxi*rr`;
				tri=inv(tr);
				ta=brr`*tri*brr/q;
				tau=ta/mse;
				probtau=1-probf(tau,q,df);
		PRINT tau q df probtau;
	FINISH joint;
	do;
		rr={1 -1 0 0 ,
			0 0 1 -1};
		r={0, 0};
	END;
USE st01;
		READ all VAR {ipg} into y;
		READ all VAR {int1 int2 spg1 spg2} into x;
RUN joint;
QUIT;
PROC IML;
	START chow;
		n = NROW(x);
		k = NCOL(x);
		df = n-k;
		xpx = x`*x;
		xpy = x`*y;
		xpxi = INV(xpx);
		b = xpxi*xpy;
		yhat = x*b;
		e = y-yhat;
		sse = e`*e;
		mse = sse/df;
	FINISH chow;

   	USE ex01;
		READ all VAR{ipg} into y;
		READ all VAR{int spg} into x;
RUN chow;
	sse0=sse;
	n0 = n;

	USE ex011;
		READ all VAR{ipg} into y;
		READ all VAR{int spg} into x;
RUN chow;
		sse1 = sse;
		mse1 = mse;
		n1=n;
		df1 = df;

	USE ex012;
		READ all VAR{ipg} into y;
		READ all VAR{int spg} into x;
RUN chow;
		sse2=sse;
		mse2=mse;
		n2=n;
		df2=df;
	/*chow testsms homoscedasticity를 전제로 하기 때문에 f-test를 선행으로 해주어야함. 그 결과가 probvf가 0.05보다 작으면
	chow test를 사용할 수 없다.*/
		vf =mse1/mse2;
		vfinv=1/vf;
		probvf=1-probf(vf, df1, df2);
		probvfinv = 1-probf(vfinv, df2, df1);
	PRINT "comparemse" mse1 mse2, "f-values are" vf vfinv;
	PRINT "probs are" probvf probvfinv;
/*pchow predictive chow*/
		dfn=n2;
		dfe=n1-k;
		numer=(sse0-sse1)/dfn;
		denom= sse1/dfe;
		pchow=numer/denom;
		probpchow = 1-probf(pchow, dfn, dfe);
	PRINT "chow" pchow probpchow;
/*CHOW*/
		dfn=k;
		dfe=n0-2*k;
		numer=(sse0-(sse1+sse2))/dfn;
		denom=(sse1+sse2)/dfe;
		chow=numer/denom;
		probchow = 1-probf(chow, dfn, dfe);
	PRINT "chow" chow probchow;
QUIT;
/*6.03*/
DATA ip;
			INFILE 'c:\data\ip.prn';
			INPUT mon ip;
			ipg = DIF(LOG(ip))*100;
RUN;
DATA sp;
			INFILE 'c:\data\sp500.prn';
			INPUT mon sp;
			spg = DIF(LOG(sp))*100;
RUN;
DATA m1;
			INFILE 'c:\data\m1_usa.prn';
			INPUT mon m1;
			IF mon > 20120701 THEN DELETE;
RUN;
DATA fyff;
			INFILE 'c:\data\fyff.prn';
			INPUT mon fy;
			fy4= LAG4(fy);
RUN;
DATA ex03;
	MERGE ip sp fyff;
	BY mon;
	WHERE 19940830 < mon < 20120801;
RUN;
DATA ex03;
	MERGE m1 ex03;
RUN;
PROC REG DATA = ex03;
		MODEL ip = m1 spg fy4;
		MODEL ip = fy4;
RUN;
PROC IML;
		USE ex03;
		READ all VAR{m1 spg fy4} INTO x;
		START fng;
			n = NROW(x);
			k = NCOL(x)+1;
			iota = J(n,1,1);
			a=I(n) - (1/n)*iota*iota`;
			ax = a*x;
			ax2 = ax#ax;
			sum = ax2[+,];
			rootsum = sum##(1/2);
			rooti = 1/rootsum;
			r = ax#rooti;
			rx = r`*r;
			drx = det(rx);
			if drx>0 THEN dr = drx;
			else dr = 1/10000;
			lnrx = log(dr);
			pow = (-1)*((n-1)-(1/6)*(2*(k-1)+5));
			chi = pow*lnrx;
			df = (1/2)*(k-1)*(k-2);
			probchi = probchi(chi,df);
			probfng =1-probchi(chi, df);
			PRINT "Farrar & Gluber test" dr lnrx chi probfng;
		FINISH fng;
	RUN fng;
QUIT;
proc autoreg data=st01; 
	model ipg=spg/chow=600 pchow=600; /*ols추정치*/ 
run;
proc autoreg data=st01;
	model ipg=spg/nlag=1 method=ml maxit=200; /*가장 많이 사용*/ 
	model ipg=spg/nlag=1;
	model ipg=spg; 
run;
