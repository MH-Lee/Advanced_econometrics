DATA ip;
			INFILE 'c:\data\ip.prn';
			INPUT mon ip;
			ipg = DIF(LOG(ip))*100;
RUN;
DATA sp;
			INFILE 'c:\data\sp500.prn';
			INPUT mon sp;
			spg = DIF(LOG(sp))*100;
RUN;
DATA fyff;
		INFILE 'c:\data\fyff.prn';
        INPUT mon fyff;
		fyff4 = LAG4(fyff);
RUN;
DATA ex01;
			MERGE ip sp fyff;
			BY mon;
			int =1;
			WHERE  19570201<=mon<=20120701;
RUN;
PROC REG DATA = ex01;
	MODEL ipg =spg fyff4/DW DWPROB;
RUN;
DATA ex02;
	SET ex01;
		IF mon < 20070101 THEN DO;
			int1 = int;
			int2 = 0;
			spg1 = spg;
			spg2 = 0;
			fyff41 = fyff4;
			fyff42 = 0;
		END;
		ELSE DO;
			int1 = 0;
			int2 = int;
			spg1 = 0;
			spg2 = spg;
			fyff41 = 0;
			fyff42 = fyff4;
		END;
RUN;
PROC REG DATA = ex02;
	MODEL ipg = int1 int2 spg1 spg2 fyff41 fyff42/NOINT;
	TEST int1=int2,spg1 = spg2,fyff41=fyff42;
RUN;
PROC IML;
	USE ex01;
		READ all VAR{ipg} into y;
		READ all VAR{int spg fyff4} into x;
		name = {"int", "spg", "fyff4"};
	START reg;
				n = NROW(x);
				k = NCOL(x);
				df = n-k; /*자유도*/
				xpx = x`*x;/*xp = xprime*/
				xpy = x`*y;
				xpxi = INV(xpx);/*역함수*/
				b = xpxi*xpy;
				yhat = x*b;
				e = y-yhat;
				sse = e`*e;
				mse = sse/df;
				covb = mse#xpxi;
				stdb = SQRT(VECDIAG(covb));
				t = b/stdb;
				probt = 1- PROBF(t#t,1,df);
				PRINT "result of regression are" name b stdb t probt;
				e1 = LAG(e,1);
				em = e-e1;
				em[1,1] = 0;
				emsq = em`*em;
				dw=emsq/sse;
				PRINT "durbin watson-d is" DW;
	FINISH reg;
	START rsquare;
				iota = J(n,1,1);
				a=i(n)-(1/n)*iota*iota`;
				ys=a*y;
				tss = ys`*ys;
				rsquare=1-sse/tss;
				denom=tss/(n-1);
				adjr2=1-mse/denom;
				PRINT "rsquare" rsquare;
				PRINT "adjusted rsquare" adjr2;
			FINISH rsquare;
		RUN reg;
		RUN rsquare;
QUIT;
PROC IML;
	USE ex02;
		READ all VAR{ipg} into y;
		READ all VAR{int1 int2 spg1 spg2 fyff41 fyff42} into x;
	START test;
		name = {"int1","int2", "spg1","spg2", "fyff41","fyff42"};
		n = NROW(x);
		k = NCOL(x);
		df = n-k; /*자유도*/
		xpx = x`*x;/*xp = xprime*/
		xpy = x`*y;
		xpxi = INV(xpx);/*역함수*/
		b = xpxi*xpy;
		yhat = x*b;
		e = y-yhat;
		sse = e`*e;
		mse = sse/df;
		br= rv*b;/*R*b*/
		q = NROW(rv);/*null hypothesis 자유도 결정*/
		rr = br - sr;
		tr = rv*xpxi*rv`;
		tri = INV(tr);
		ta = rr`*tri*rr/q;
		tau = ta/mse;
		probtau = 1-PROBF(tau, q, df);
		PRINT tau q df probtau;
		FINISH test;
		DO;
			rv = {1 -1 0 0 0 0,
					0 0 1 -1 0 0,
					0 0 0 0 1-1};
			sr = {0, 0, 0};
		END;
	RUN TEST;
QUIT;
DATA ex03;
	MERGE ip sp;
	BY mon;
	int = 1;
	WHERE 19570101 < mon < 20120801;
RUN;
PROC REG DATA=ex03;
	MODEL ipg = spg/DW DWPROB;
	OUTPUT OUT = out02 R = resid;
RUN;
PROC IML;
	START reg;
				n = NROW(x);
				k = NCOL(x);
				df = n-k;
				xpx = x`*x;
				xpy = x`*y;
				xpxi = INV(xpx);
				b = xpxi*xpy;
				yhat = x*b;
				e = y-yhat;
				sse = e`*e;
				mse = sse/df;
				covb = mse#xpxi;
				stdb = SQRT(VECDIAG(covb));
				t = b/stdb;
				probt = 1- PROBF(t#t,1,df);
				PRINT "result of regression are" name b stdb t probt;
				e1 = LAG(e,1);
				em = e-e1;
				em[1,1] = 0;
				emsq = em`*em;
				dw=emsq/sse;
				PRINT "durbin watson-d is" dw;
	FINISH reg;
	USE ex03;
		READ all VAR{ipg} into y;
		READ all VAR{int spg} into x;
	RUN reg;
QUIT;
DATA ex04;
		SET out02;
		resid1=LAG(resid);
RUN;
PROC REG DATA=ex04;
		MODEL resid = resid1;
RUN;
